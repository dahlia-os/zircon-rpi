# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

###############################################################################
# PACKAGES
###############################################################################
# TODO(49566): switch to fuchsia_package once using feedback-data as package
# name.
package("feedback_agent") {
  binaries = [
    {
      name = "feedback_agent"
    },
    {
      name = "system_log_recorder"
    },
  ]

  meta = [
    {
      path = "meta/feedback_agent.cmx"
      dest = "feedback_agent.cmx"
    },
  ]

  resources = [
    {
      path = "config.json"
      dest = "config.json"
    },
  ]

  deps = [
    ":main",
    "//src/developer/forensics/feedback_data/system_log_recorder",
  ]
}

###############################################################################
# EXECUTABLES
###############################################################################
executable("main") {
  output_name = "feedback_agent"

  sources = [ "main.cc" ]

  deps = [
    ":main_service",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

###############################################################################
# SOURCES
###############################################################################
source_set("config") {
  sources = [
    "config.cc",
    "config.h",
  ]

  public_deps = [
    "//src/developer/forensics/feedback_data/annotations:types",
    "//src/developer/forensics/feedback_data/attachments:types",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/files",
    "//third_party/rapidjson",
  ]
}

source_set("constants") {
  sources = [ "constants.h" ]

  public_deps = [ "//sdk/fidl/fuchsia.feedback" ]

  visibility = [ "//src/developer/forensics/feedback_data/*" ]
}

source_set("data_provider") {
  public = [ "data_provider.h" ]

  sources = [
    "data_provider.cc",
    "data_provider.h",
    "image_conversion.cc",
    "image_conversion.h",
  ]

  public_deps = [
    ":integrity_reporter",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/developer/forensics/utils/cobalt",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]

  deps = [
    ":constants",
    ":datastore",
    "//garnet/public/lib/fostr/fidl/fuchsia.images",
    "//sdk/fidl/fuchsia.images",
    "//sdk/fidl/fuchsia.mem",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data/annotations:types",
    "//src/developer/forensics/feedback_data/annotations:utils",
    "//src/developer/forensics/feedback_data/attachments:attachment_providers",
    "//src/developer/forensics/feedback_data/attachments:types",
    "//src/developer/forensics/feedback_data/attachments:utils",
    "//src/developer/forensics/utils:archive",
    "//src/developer/forensics/utils/fit",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//third_party/libpng",
  ]
}

source_set("datastore") {
  sources = [
    "datastore.cc",
    "datastore.h",
  ]

  public_deps = [
    "//sdk/lib/sys/cpp",
    "//src/developer/forensics/feedback_data/annotations:annotation_providers",
    "//src/developer/forensics/feedback_data/annotations:types",
    "//src/developer/forensics/feedback_data/attachments:types",
    "//src/developer/forensics/utils/cobalt",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]

  deps = [
    ":constants",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data/annotations:annotation_providers",
    "//src/developer/forensics/feedback_data/annotations:static_annotations",
    "//src/developer/forensics/feedback_data/attachments:attachment_providers",
    "//src/developer/forensics/feedback_data/attachments:static_attachments",
  ]
}

source_set("data_register") {
  sources = [
    "data_register.cc",
    "data_register.h",
  ]

  public_deps = [
    ":constants",
    ":datastore",
    "//sdk/fidl/fuchsia.feedback",
    "//src/developer/forensics/feedback_data/annotations:types",
    "//src/lib/fxl",
  ]

  deps = [ "//sdk/lib/syslog/cpp" ]
}

source_set("device_id_provider") {
  sources = [
    "device_id_provider.cc",
    "device_id_provider.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//src/developer/forensics/feedback_data/annotations:types",
  ]

  deps = [
    ":constants",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/utils:errors",
    "//src/lib/files",
    "//src/lib/fxl",
    "//src/lib/uuid",
  ]
}

source_set("errors") {
  sources = [ "errors.h" ]

  public_deps = [
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/utils:errors",
  ]
}

source_set("main_service") {
  sources = [
    "main_service.cc",
    "main_service.h",
  ]

  public_deps = [
    ":config",
    ":data_provider",
    ":data_register",
    ":datastore",
    ":device_id_provider",
    ":inspect_manager",
    "//src/developer/forensics/utils/cobalt",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]

  deps = [
    ":constants",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/public/lib/zx",
  ]
}

source_set("inspect_manager") {
  public = [ "inspect_manager.h" ]

  sources = [
    "inspect_manager.cc",
    "inspect_manager.h",
  ]

  public_deps = [ "//src/developer/forensics/utils:inspect_node_manager" ]

  deps = [ "//src/developer/forensics/utils:inspect_protocol_stats" ]
}

source_set("integrity_reporter") {
  sources = [
    "integrity_reporter.cc",
    "integrity_reporter.h",
  ]

  public_deps = [
    "//src/developer/forensics/feedback_data/annotations:types",
    "//src/developer/forensics/feedback_data/attachments:types",
    "//zircon/public/lib/fit",
  ]

  deps = [
    ":errors",
    "//src/developer/forensics/utils:errors",
    "//third_party/rapidjson",
  ]
}
