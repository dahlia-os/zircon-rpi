# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test/test_package.gni")
import("//src/lib/isolated_devmgr/devmgr_manifest.gni")

group("tests") {
  testonly = true
  deps = [ ":block-verity-tests" ]
}

source_set("geometry") {
  sources = [ "geometry.cc" ]
}

driver_module("block-verity") {
  output_name = "block-verity"

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  sources = [
    "device-info.cc",
    "device-manager.cc",
    "device.cc",
    "extra.cc",
  ]

  configs += [ "//build/config/fuchsia:integer-paranoia" ]

  deps = [
    ":geometry",
    "//sdk/banjo/ddk.protocol.block",

    #"//sdk/banjo/ddk.protocol.block.partition",
    #"//sdk/banjo/ddk.protocol.block.volume",
    "//sdk/fidl/fuchsia.hardware.block.verified:fuchsia.hardware.block.verified_llcpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",

    #"//zircon/public/lib/bitmap",
    #"//zircon/public/lib/fbl",
    #"//zircon/public/lib/pretty",
    #"//zircon/public/lib/sync",
    #"//zircon/public/lib/zx",
    #"//zircon/system/ulib/zircon-crypto:zircon-crypto-static",
  ]
}

executable("block-verity-test") {
  testonly = true
  output_name = "block-verity-test"
  sources = [ "block-verity-test.cc" ]
  deps = [
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/driver-integration-test",
    "//zircon/system/ulib/ramdevice-client",
    "//zircon/system/ulib/fvm-devices",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
  ]
}

executable("geometry-test") {
  testonly = true

  output_name = "geometry-test"
  sources = [ "geometry-tests.cc" ]
  deps = [
    ":geometry",
    "//zircon/public/lib/zxtest",
  ]
}

devmgr_manifest("devmgr-manifest") {
}

test_package("block-verity-tests") {
  tests = [
    {
      name = "geometry-test"
      environments = basic_envs
    },
    {
      name = "block-verity-test"
      environments = basic_envs
    },
  ]

  deps = [
    ":block-verity",
    ":block-verity-test",
    ":devmgr-manifest",
    ":geometry-test",
  ]

  extra = [ "$target_gen_dir/devmgr.manifest" ]
}
