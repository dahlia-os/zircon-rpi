# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

library("zbitl") {
  host = true
  kernel = true
  static = true

  sources = [
    "check.cc",
    "item.cc",
  ]
  deps = [ "$zx/third_party/ulib/cksum" ]
  public_deps = [
    # <lib/zbitl/view.h> has #include <lib/cksum.h>.
    "$zx/third_party/ulib/cksum:headers",
  ]

  if (!is_kernel) {
    sources += [
      "fd.cc",
      "stdio.cc",
    ]
    deps += [ "$zx/system/ulib/fbl" ]
    public_deps += [
      # <lib/zbitl/fd.h> has #include <fbl/unique_fd.h>.
      "$zx/system/ulib/fbl:headers",
    ]

    if (is_fuchsia) {
      sources += [ "vmo.cc" ]
      deps += [ "$zx/system/ulib/zx" ]
      public_deps += [
        # <lib/zbitl/vmo.h> has #include <lib/zx/vmo.h>.
        "$zx/system/ulib/zx:headers",
      ]
    }
  }
}
